apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    signingConfigs {
        YYBKey {
            keyAlias        project.YYB_KEY_ALIAS
            keyPassword     project.YYB_KEY_PWD
            storeFile       file(project.YYB_FILE_PATH)
            storePassword   project.YYB_STORE_PWD
        }
        KuAnKey {
            keyAlias        project.KUAN_KEY_ALIAS
            keyPassword     project.KUAN_KEY_PWD
            storeFile       file(project.KUAN_FILE_PATH)
            storePassword   project.KUAN_STORE_PWD
        }
    }

    compileSdkVersion 27
    defaultConfig {
        applicationId "com.wxm.shopassistant"
        minSdkVersion 24
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res',
                            'src/main/res/layouts/fragment',
                            'src/main/res/layouts/page',
                            'src/main/res/layouts/dialog',
                            'src/main/res/layouts/viewItem',
                            'src/main/res/layouts/base',
                            'src/main/res/layouts'
                    ]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }

        debug {
            debuggable true
        }
    }

    flavorDimensions "default"
    productFlavors {
        alpha {}

        YYB_keepaccount {
            signingConfig signingConfigs.YYBKey
        }

        KuAn_keepaccount {
            signingConfig signingConfigs.KuAnKey
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${variant.versionName}-${releaseTime()}.apk"
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+8"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'

    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'com.trello:rxlifecycle:0.5.0'
    implementation 'com.trello:rxlifecycle-components:0.5.0'
    implementation 'com.squareup.retrofit2:retrofit:2.0.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.0.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.0.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.squareup.okhttp3:okhttp:3.7.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'io.reactivex:rxandroid:1.1.0'
    implementation 'org.apache.httpcomponents:httpcore:4.4.4'

    implementation 'com.theartofdev.edmodo:android-image-cropper:2.7.+'

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.0'
    implementation 'com.jakewharton:butterknife:8.8.0'
    implementation 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'

    implementation files('libs/extend_lib/fashjson/fastjson-1.2.33.jar')
    implementation files('libs/extend_lib/junit/junit-4.12.jar')
    implementation files('libs/extend_lib/sqlite_jdbc/sqlite-jdbc-3.8.11.jar')

    implementation project(':androidUtil')
    implementation project(':uiLib')
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}